#Todos los monumentos de un distrito:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?name
WHERE {
    ?district :name "CENTRO" .
    ?monument :isLocatedInDistrict ?district ;
              :name ?name .
}


#Todos los monumentos de un barrio:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?name
WHERE {
    ?neighborhood :name "GOYA" .
    ?monument :isLocatedInNeighborhood ?neighborhood ;
              :name ?name .
}


#Monumento a partir de su nombre:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?date ?address ?description 
WHERE {
    ?monument :name "Conjunto escultórico parque Arquitecto Antonio Palacios" ;
              :date ?date ;
              :address ?address ;
              :description ?description ;
  			  rdf:type ?type .
  	      	  ?type rdfs:label ?typeLabel
}

#Monumento a partir de su nombre aproximado:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?typeLabel ?name ?date ?address ?description 
WHERE {
    ?monument :name ?name ;
    		  :date ?date ;
              :address ?address ;
              :description ?description ;
              rdf:type ?type .
  	      ?type rdfs:label ?typeLabel
    FILTER(CONTAINS(LCASE(?name), "parque arquitecto antonio palacios")) .
}

#Monumentos construidos en un año aproximado:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?typeLabel ?name ?date ?address ?description 
WHERE {
    ?monument :name ?name ;
    		  :date ?date ;
              :address ?address ;
              :description ?description ;
              rdf:type ?type .
  	      ?type rdfs:label ?typeLabel
    FILTER(CONTAINS(LCASE(?date), "1990")) .
}

# Todos los monumentos del barrio Goya con sus propiedades:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?name ?typeLabel ?date ?address ?description ?postalCode
WHERE { 
    ?neighborhood :name "GOYA" .
    ?monument :isLocatedInNeighborhood ?neighborhood ;
     	rdf:type ?type .
	?type rdfs:label ?typeLabel .
    ?monument :name ?name .
    # Cada propiedad en su propio bloque OPTIONAL. De lo contrario,
    # si una no está, no se mostrarán las demás propiedades.
    OPTIONAL { ?monument :postalCode ?postalCode . }
    OPTIONAL { ?monument :description ?description . }
    OPTIONAL { ?monument :date ?date . }
    OPTIONAL { ?monument :address ?address . }
}

#Todos los monumentos de un/a autor/a:
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?monument ?name ?typeLabel ?date ?address ?postalCode ?description
WHERE { 
    ?author :name "ANTONINI, Lucía" .
    ?monument :hasAuthor ?author ;
     	rdf:type ?type .
	?type rdfs:label ?typeLabel .
    ?monument :name ?name .
    OPTIONAL { ?monument :postalCode ?postalCode . }
    OPTIONAL { ?monument :description ?description . }
    OPTIONAL { ?monument :date ?date . }
    OPTIONAL { ?monument :address ?address . }
}


#Consulta federada con Wikidata para obtener información acerca del personaje representado en una estatua:
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?name ?typeLabel ?date ?address ?depictedPerson ?depictedPersonImage 
	?depictedPersonCitizenshipLabel ?dateOfBirth ?placeOfBirth ?description
WHERE {
	BIND("400070" AS ?catalogCode) # catalogCode = "400070"
	# Desde la ontología de monumentos:
	?monument :name ?name ;
		:PK ?catalogCode ;
		rdf:type ?type .
		?type rdfs:label ?typeLabel .
	OPTIONAL { ?monument :description ?description . }
	OPTIONAL { ?monument :date ?date . }
	OPTIONAL { ?monument :address ?address . }

	# Desde Wikidata
	SERVICE <https://query.wikidata.org/sparql> {
		?wikidataMonument wdt:P528 ?catalogCode ; # Propiedad catalogCode de Wikidata
			     wdt:P180 ?depictedPerson .
		OPTIONAL { 
			?depictedPerson wdt:P569 ?dateOfBirth .
			?depictedPerson wdt:P19 ?placeOfBirth .
		}
		OPTIONAL {
			?depictedPerson wdt:P18 ?depictedPersonImage
		}
		OPTIONAL {
			?depictedPerson wdt:P27 ?depictedPersonCitizenship .
			?depictedPersonCitizenship rdfs:label ?depictedPersonCitizenshipLabel .
			FILTER(LANG(?depictedPersonCitizenshipLabel) = "es")  
		}
		OPTIONAL {
			?depictedPerson rdfs:label ?depictedPersonLabel
			FILTER(LANG(?depictedPersonLabel) = "es")
		}
	}
}

#Consulta geoespacial con st_distance:
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX bif: <bif:>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments#>

SELECT (bif:st_distance(?wkt, ?refPoint) AS ?distance) ?monument 
       ?name ?typeLabel ?date ?address SUBSTR(?description, 1, 50) AS ?shortDescription ?geometry 
FROM <http://www.semanticweb.org/vic/ontologies/2024/10/monuments>
WHERE {
  BIND(bif:st_point(-3.6042, 40.4076) AS ?refPoint)
  ?geometry geo:asWKT ?wkt .
  FILTER(bif:st_distance(?wkt, ?refPoint) < 0.01) .
  ?monument :hasGeometry ?geometry ;
            :name ?name ;
            rdf:type ?type .
  OPTIONAL { ?type rdfs:label ?typeLabel . }
  OPTIONAL { ?monument :date ?date . }
  OPTIONAL { ?monument :address ?address . }
  OPTIONAL { ?monument :description ?description . }
}
ORDER BY ?distance

#Consulta geoespacial con st_within:
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX bif: <bif:>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <http://www.semanticweb.org/vic/ontologies/2024/10/monuments#>

SELECT ?monument ?name ?typeLabel ?date ?address 
       (STR(SUBSTR(?description, 1, 50)) AS ?shortDescription) ?geometry
FROM <http://www.semanticweb.org/vic/ontologies/2024/10/monuments>
WHERE {
  BIND(bif:st_point(-3.6042, 40.40766) AS ?refPoint)
  ?geometry geo:asWKT ?wkt .
  FILTER(bif:st_within(?wkt, ?refPoint, 0.05)) .
  ?monument :hasGeometry ?geometry ;
            :name ?name ;
            rdf:type ?type .
  OPTIONAL { ?type rdfs:label ?typeLabel . }
  OPTIONAL { ?monument :date ?date . }
  OPTIONAL { ?monument :address ?address . }
  OPTIONAL { ?monument :description ?description . }
}

